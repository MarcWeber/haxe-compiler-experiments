version = $loader.args[0];
if( version == null )
	$throw("<version> argument required");

sys = $loader.loadprim("std@sys_string",0)();
binext = "";
ext = switch( sys ) {
	"Windows" => {
		binext = ".exe";
		""
	}
	"Mac" => "-osx"
	"Linux" => "-linux"
	"BSD" => "-bsd"
	default => $throw("Unknown system")
}

rights = 493; // octal 755
_mkdir = $loader.loadprim("std@sys_create_dir",2);
mkdir = function(d) {
	_mkdir(d,rights);
}

chdir = $loader.loadprim("std@set_cwd",1);

_cmd = $loader.loadprim("std@sys_command",1);
cmd = function(c) {
	if( _cmd(c) != 0 ) $throw("Command '"+c+"' failed");
}

dir = "haxe-"+version+ext;

cmd("rm -rf "+dir);
mkdir(dir);
mkdir(dir+"/doc");

if( sys == "Windows" )
	cmd("cp ../haxe.exe ../haxesetup.exe haxeserver.bat "+dir);
else
	cmd("cp ../haxe "+dir);

cmd("cp -R CHANGES.txt LICENSE.txt ../std "+dir);

chdir(dir+"/std");
cmd("haxe all.hxml");
chdir("tools");

// BUILD TOOLS

chdir("haxedoc");
cmd("haxe haxedoc.hxml");
cmd("./haxedoc \"../../flash.xml;flash\" \"../../neko.xml;neko\" \"../../js.xml;js\"");
cmd("mv index.html content ../../../doc");
cmd("mv haxedoc"+binext+" ../../..");
chdir("..");

chdir("haxelib");
cmd("haxe haxelib.hxml");
cmd("mv haxelib"+binext+" ../../..");
chdir("..");

// CLEANUP

chdir("..");

cmd("rm -rf CVS .cvsignore */CVS */.cvsignore */*/CVS */*/.cvsignore */*/*/CVS */*/*/.cvsignore");
cmd("rm -rf all.n all.js *.swf *.xml");

chdir("tools");
chdir("haxedoc");
cmd("rm -rf haxedoc.n index.html content haxedoc"+binext);
chdir("..");

chdir("haxelib");
cmd("rm -rf *.bat *.zip *.db *.n tmp files");
chdir("..");

chdir("..");
cmd("rm -rf mt mtwin");
chdir("../..");

// PACKAGE

if( sys == "Windows" ) {
	// add neko
	chdir("../../neko/bin");
	cmd("cp gc.dll neko.dll neko.exe nekoc.exe nekotools.exe *.ndll ../../haxe/doc/"+dir);
	chdir("../../haxe/doc");
	cmd("7z a -tzip "+dir+".zip "+dir);
}
else
	cmd("tar -czf "+dir+".tar.gz "+dir);

cmd("rm -rf "+dir);
